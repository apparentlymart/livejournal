
### Set up layer
layerinfo "type" = "core";
layerinfo "name" = "LiveJournal S2 Base Layer";
layerinfo "uniqname" = "core";
layerinfo "majorversion" = "1";
layerinfo "author_name" = "LiveJournal Webmaster";
layerinfo "author_email" = "webmaster@livejournal.com";

### Built-in stuff
# properties
#property builtin string IMGDIR; #                          "A URL which can be safely prepended onto a path starting with / where sitewide image resources can be found.";
#property builtin string SITENAME; #                        "Actual name of the host site.";
#property builtin string SITEROOT;                       # Site URL which can be safely prepended onto paths starting with /
#property builtin string SITEURL;                        # A full URL for the host site (with trailing slashes where necessary)
property string IMGDIR; #                          "A URL which can be safely prepended onto a path starting with / where sitewide image resources can be found.";
property string SITENAME; #                        "Actual name of the host site.";
property string SITEROOT;                       # Site URL which can be safely prepended onto paths starting with /
property string SITEURL;                        # A full URL for the host site (with trailing slashes where necessary)


class string
{
  function builtin substr(int start, int length) : string
  "Returns up to \$length characters from string, skipping \$start characters from the beginning.";

  function builtin ends_with (string sub) : bool
  "Returns true if string ends in \$sub";

  function builtin starts_with (string sub) : bool
  "Returns true if string begins with \$sub";

  function builtin contains (string sub) : bool
  "Return true if string contains \$sub";

  function builtin lower : string
  "Returns string in lower case.";

  function builtin upper : string
  "Returns string in upper case";

  function builtin upperfirst : string
  "Return string with the first character capitalized.";

  function builtin length() : int
  "Return the number of characters in the string.";

  function builtin repeat(int n) : string
  "Returns the string repeated n times";

  function builtin asciionly() : string
  "Return a version of the string with only USASCII characters present ( character codes <= 127 ). Useful if you are giving output in a format with no unicode support, but will only work for English text.";

  # TODO: Overload with an extra parameter specifying a number system to convert to,
  #      and maybe a way to get an ordinal rather than cardinal number?
  function builtin insert_number(int num) : string
  "Insert the number into the string at the point where \"#\" is found. Used to add numerals to customizable text properties.";

}

class int {
    function builtin toString() : string; # Heh... guess what the perl code which implements this does? ;)
    function builtin compare(int other) : int; # Does $this <=> $other
}

# classes
class Date {
    var int year;
    var int month;
    var int day;
    var int hour;
    var int minute;
    var int second;             # Will often be zero since LJ doesn't store seconds
    function builtin dayofweek() : int; # Returns the day of the week this date falls on, where Monday=1, Sunday=7
    function date_short() : string;      # Outputs a short date in a format defined in either i18n or user
    function date_long() : string;       # Outputs a long date in a format defined in either i18n or user
    function time_short() : string;      # Outputs a short time
    function time_long() : string;       # Outputs a long time (might just be the same as short time in some i18ns)
    function datetime_short() : string;  # Outputs a representation of the current date and time
    function datetime_long() : string;   # Outputs a representation of the current date and time
    function builtin date_uniq() : string "'Machine-readable' date - yyyymmdd.";
    function builtin time_uniq() : string "'Machine-readable' time - hhmmss.";
    function builtin datetime_uniq() : string "'Machine-readable' date/time - yyyymmddhhmmss.";
    function builtin datetime_unix() : string "Unix timestamp representing this date/time.";
    function toString() : string;
}
class Color {
    var int r;
    var int g;
    var int b;
    function builtin Color(string s) : Color;
    function builtin brighter() : Color;                # make color brighter, by 5%
    function builtin brighter(int p) : Color;           # make color brighter, by p percent
    function builtin darker() : Color;                  # make color darker, by 5%
    function builtin darker(int p) : Color;             # make color darker, by p percent
    function builtin inverse() : Color;                 # invert color
    function builtin toString() : string;
}

### Core

# classes
class Image {                   ### A HTML embedded image
    var string url;             # URL of the image
    var int w;                  # Width in pixels
    var int h;                  # Height in pixels
    var string alt;             # Alternative text
    var string{} attributes;    # Some extra HTML attributes to add to the image tag.
    var string title;           # Title of this image (usually blank)
    var string longdesc;        # Long description of this image (usually blank)
    function print();            # Output some HTML representing this image
    function toString() : string; # Return some HTML representing this image
}
class Link {                    ### A link or button
    var string text;            # Initially this will be either blank, equal to id, or
                                  # just be a suggestion (in English) from the server for the label of the link.
                                  # A function which uses this class should first redefine all of the ids
                                  # it knows about, eg in a Spanish i18n layer, $.views{'recent'}="Entradas recientes", or
                                  # take a value from a string property.
    var string id;              # A simple textual id that will never change for this function. Can be matched with an if or used with a hash of Link. (eg: recent, editentry)
    var string url;             # URL to link to
    var Image icon;             # A suggestion from the server as to which icon to use. layouts/users can override this of course. alt text works similarly to $.text .
    function print_button;     # Output this Link as a clickable button using the image
    function print_text;       # Output this Link as a simple HTML link.
    function toString() : string;
}
class UserLite {                ### A 'lite' userinfo structure which only requires one db query to build
    var string username;
    var int userid;
    var string name;            # User's 'real name'
    var string baseurl;         # Base URL of this user's journal
    var string journaltype;     # P,C,N: Personal, Community, News (can be used for generating different icons, etc)
    function toString() : string; # Return a standard <lj user="..."> type of thing.
    function toString(Color fg,Color bg,bool usefg,bool usebg) : string; # Master tostring function - for all of your user output needs
    function print();
    function print(Color fg);
    function print(Color fg,Color bg);
}
class MetaData {                ### Metadata attached to something
    var string key              "The internal name for the given metadata item. For example, 'currentmusic', 'currentmood'.";
    var string val;
    function print();
    function indicator() : string; # Return the indicator for this metadata... eg, "Current Music" or the equivalent in other languages
}
class CommentInfo {             ### Information about comments attached to something
    var string url_read;
    var string url_post;
    var int comment_count;
    var bool enabled;           # Set to false if comments disabled journal-wide or just on this item.
    function print();
    function print_readlink;
    function print_postlink;
}
class UserPic {                 ### Information about userpics (doesn't extend Image because we want different semantics)
    var int picid;              # Unique id number of this picture
    var string[] keywords;      # A list of picture keywords which currently match this picture
    var string selkeyword;      # The keyword which (when associated with an entry or comment) caused this picture to be displayed
    var string url;             # URL which can be used to display the picture
    var int width;              # The width of the userpic in pixels
    var int height;             # The height of the userpic in pixels
    var bool default;           # Is this picture the users' default?
    function print();            # Outputs some basic HTML to display this userpic
    function print(string align); # Outputs some HTML to display this userpic with the specified alignment.
    function toString() : string;
    function toString(string align) : string;
}
class Attachment {              ### A file attached to an entry
    var string filename;        # The filename of the attached file
    var string description;     # An optional description of what the file is
    var string url;             # A URL to link to in order to download this file.
    var string type;            # A MIME-style content-type specification (eg text/html, application/x-livejournal-s2layer)
    var Image icon;             # An icon which can be used to link to this file
    var int size;               # The size of the file, in bytes
    var string nicesize;        # The size of the file in appropriate units with indicator, eg "10MB", "240k", "4 bytes"
    function print_full();     # Output a "row"-format link to this attachment
    function print_small();    # Output a more compact link - eg, a small iconic link with no description.
}
class Entry {                   ### A journal entry
    var string subject;
    var string text;            # Will be blank on some viewtypes eg 'month' view
    var string lang             "The language of this journal entry, which should be used in an HTML 'lang' attribute on the entry's outermost tag.";
    var string textdirection    "The text direction of this journal entry, which should be used in an HTML 'dir' attribute on the entry's outermost tag.";
    var Date entrytime;
    var Date servertime;
    var string security;        # 'public', 'private', 'friends' or 'custom'.
    var Image securityicon;     # A little icon which should be displayed somewhere on an entry to represent the security setting
    var bool has_security;      # True if the entry is private or protected
    var UserLite poster;
    var UserLite journal;
    var bool has_attachments;   # True if there are actually any members attachments
    var bool has_metadata;      # True if there are actually any members in metadata
    var MetaData{} metadata;
    var CommentInfo commentinfo;
    var bool new_day;           # Is this entry on a different day to the previous one?
    var bool end_day;           # Is this the last entry of a day?
    var UserPic userpic;        # The userpic selected to relate to this entry
    var Link{} links;           # An array of links that the server suggests be linked from this entry, eg to edit an entry if a user is viewing their own journal.
    var Link previousentry;     # A link to the previous entry in this user's journal
    var Link nextentry;         # A link to the next entry in this user's journal
    var int itemid;
    var string uniqueid;        # An alphanumeric ID for the entry which is guaranteed to be globally unique (probably username-12354)
    var string viewurl;         # A URL at which this specific entry can be viewed, for linking purposes. ***
    var Attachment[] attachments; # An array of all of the files attached to this entry.
    function set_link_text();     # Call for translation/customisation of the labels in 'links'.
    function print_metadata(); # Print out the metadata
    function print_attachments(); # Print out the attachment list
}
class Comment {                 ### A comment on something (only journal entries right now)
    var int talkid;             # The unique id of this comment.
    var int parenttalkid;       # The unique id of this comment's parent.
    var UserLite poster;        # The person who posted this comment
    var string state;           # "A","D" ... Normal and Deleted respectively. (If D, almost everything else will be blank)
    var Date servertime;        # The date and time this entry was made, according to the server clock.
    var Link{} links;           # An array of links that the server suggests be linked from this comment, eg to delete it.
    var string subject;         # The subject line from the comment.
    var string text;            # The actual text of the entry
    var string lang             "The language of this journal entry, which should be used in an HTML 'lang' attribute on the entry's outermost tag.";
    var string textdirection    "The text direction of this journal entry, which should be used in an HTML 'dir' attribute on the entry's outermost tag.";
    var UserPic userpic;        # The userpic selected for this comment
    var Link parentlink;        # A URL which can be used to see the parent comment.
    var Link replylink;         # A URL which can be used to post a reply to this comment.
    var Link viewlink;          # A URL which can be used to view this particular comment.
    var Link threadlink;        # A URL which can be used to view the thread beginning with this comment.
    var bool collapsed;         # When there are lots of comments, some will be collapsed into links to view them.
    var bool collapsemode;      # When there are lots of comments and some are collapsed, this will be true. You may wish to show thread expansion links in this case.
    var int indent;             # When displaying a conversation thread, you should multiply this value by something to make threads indent.
    function set_link_text();     # Call for translation/customisation of the labels in 'links' and the other Link members.
    function print();          # Output the entry in the most appropriate format (based on $.collapsed)
    function print_full();     # Write out the entry in full.
    function print_collapsed();# Write out the entry in collapsed format.
    function print_statetext();# Write out words representing $.state, eg "Reply from Suspended User".
    function print_taglinetext(); # Output the text for the 'tagline' - username, date.
}
class Interest {                ### An interest held by a user
    var string name;            # The texual name of the interest
    var string url;             # A URL which can be visited for information about this interest
    var int count;              # A count of how many users hold this interest
    var bool shared;            # The user viewing the page also holds this interest
}
class PortalBox {               ### Representation of an LJ portal box
    var string name;            # Internal name for this portalbox
    var string title;           # The title of the portal box
    function builtin print_content(string size); # Print out the content of this portal box, preferably in the specified size.
                                                  #if the size is not supported, fall back on one that is.
    function builtin print_head(); # Run the 'head' handler for this portalbox.
    function print(string size); # Output this portal box, preferably in the size specified.
    function print();          # Default to small
}
class Properties {              ### A collection of properties for a particular 'thing' (user, entry, comment, whatever)
    var string key;             # An identifier for whatever this is the properties for.
    var string{} item;          # A hash of property values, indexed by their names (eg, $someprops.item{'url'}, or $up{'frank'}.item{'urlname'})
}
class Gender {
    var string internalname;    # A code used to represent this gender internally
    function name : string;     # Return the gender name in natural language
    function posessive : string;# Return a posessive pronoun eg 'his', 'her'.
    function pronoun : string;  # Return a pronoun eg 'he', 'she'.
    function toString() : string;
}
class Friend {                  ### Represents a friends or friendof list
    var int userid;             # Userid of friend
    var string username;        # Username of friend
    var string name;            # Full Name of friend
    var Color bgcolor;          # Background color selected for friend
    var Color fgcolor;          # Foreground color selected for friend
    var UserLite info;          # A 'UserLite' structure representing this friend.
    function toString() : string;
}
class User extends UserLite {   ### A more information-rich userinfo structure
    var UserPic defaultpic;     # Information about default userpic
    var Date birthday;
    var Gender gender;
    var string websiteurl;
    var string websitename;
    var string email;           # Email address of user
    var string location;        # A string representing this user's location as specified in their settings
    var bool hasinterests;      # Does the user have any interests?
    var bool hasfriends;        # Does the user have any friends?
    var bool hasfriendof;       # Does the user have any friendofs? (or are friendofs visible?)
    var bool hascommunitymembership; # Is the user a member of any communities? (or, does this community have any members?)
    var bool hascommunitieswatched; # Does the user watch any communities? (or, is this community watched by anyone?)
    var int caps;               # The user's 'capability class' settings, which should be used with lang_accountname to get an account type
    function builtin getinterests() : Interest[]; # Returns an array of interests the user has.
    function builtin getfriends() : Friend{}; # Returns a hash representing this users' friends list.
    function builtin getfriendof() : Friend{}; # Returns a hash representing this users' friendof list.
    function builtin getcommunitymembership() : Friend{}; # Returns a hash of communities this user is a member of
    function builtin getcommunitieswatched() : Friend{}; # Returns a hash of communities this user lists as a friend
    function builtin getbio() : string; # Returns the users' bio, or nothing if one isn't available.
    function builtin getcreatetime() : Date; # Returns the date and time the account was created
    function builtin getupdatetime() : Date; # Returns the date and time the account was last updated
    function builtin getuserpickw() : UserPic{}; # Information about userpics by keyword (eg: $pics{'happy'})
    function builtin getuserpicid() : UserPic{}; # Information about userpics by id (eg: $pics{'10234'})
    function builtin getuserprops(string[] names) : string{}; # Load information from userprops into a hash
    function builtin makelite() : UserLite; # Get a UserLite object representing this user
}
class RangeInfo {               ### Information regarding the selected range on a recent view
    var int shown;              # Number of entries included in the entries array
    var int skipped;            # The number of entries skipped into the past
    var int max_skip;           # The largest skip value allowed
    var string backurl;         # The URL to use to skip backwards (blank if this is not possible here)
    var string forwardurl;      # The URL to use to skip forwards (blank if this is not possible here)
    var int backnum;            # The number of entries which will be skipped backward by the given URL
    var int forwardnum;         # The number of entries which will be skipped forward by the given URL
}
class Page {
    var string view;
    var User journal;           # User whose journal is being viewed
    var UserLite viewer;        # User who is viewing the journal now (if journal.userid==viewer.userid then user is viewing their own journal)
    var bool viewer_is_friend;  # True if the user described in $.viewer is a friend of the user described in $.journal
    var string journaltype;     # P,C,N: Personal, Community, News
    var string baseurl;         # The base URL of the journal being viewed.
    var Link{} views;           # Links to top-level views where id equals the name of the view being linked to. (if one of views == $.view, already looking at that view)
    var string[] viewsorder;    # An array of view identifiers which can be used to order the views hash.
    var string head_content     "Extra tags supplied by the server to go in the <head> section of the output HTML document. Layouts should include this in the head section if they are writing HTML.";
    var string lang             "The primary language of this journal. Should be used in an HTML 'lang' attribute of the 'html' tag.";
    var string textdirection;   # The primary text direction of this journal (rtl or ltr)
    var PortalBox[] portalboxes;# An array, initially empty, of portalboxes loaded by loadportalbox(name) in the order loaded.
    var string stylesheeturl;   # The URL to use in a link element for the server-supported external stylesheet (define external_stylesheet() to put stuff in it)
    var string global_title     "A title selected by the user for their whole journal.";

    function print
    "The main entry point that LiveJournal calls. Code execution starts here. Layouts should override this to create HTML that's the same for all view types, and use \$this->title, \$this->head and \$this->body to include view-specific content into the template.";

    function view_title : string
    "Return a title for this particular page, such as \"Friends' Recent Entries\" for the friends view, or a date for the day view. Should be overridden in i18n layers. Ideally, layout layers should never touch this — see [member[Page.title]].";

    function title : string    "Return a relevant combination of [member[global_title]] and [method[Page::view_title()]]. Should be overridden in layout layers.";
    function print_head        "Print extra data into the head section of the output. Should be overridden in the user layer.";
    function print_body;        # dump body of page
    function print_entry(Entry e) "Output a journal entry. Layouts should override this and the inherited versions in RecentPage, FriendsPage and DayPage to change how entries display.";
    function print_entry_summary(Entry e) "Output a journal entry with no entry text. Layouts should override this for CalendarMonthPage and use it to produce the entry references on that page.";
    function print_entry_poster(Entry e) "Output a line of text which says who posted an entry (just \"user\", or \"user posting in somejournal\")";
    function set_view_text()   "Call for initialisation/translation of view link text. The backend will call this for you before rendering begins.";
    function builtin load_portal_box(string name) : PortalBox "Loads a given portal box and returns a PortalBox object representing its content, which you can later output.";
    function builtin load_portal_box(string name,string{} parameters) : PortalBox "Loads a given portal box with given box-specific parameters and returns a PortalBox object representing its content, which you can later output.";
    function builtin get_this_month() : CalendarPageMonth "Returns information about the current month, so that the page may include a mini-calendar or similar features.";
    function print_viewspecific_links() "Print view-specific navigation links, if any (such as page skipping links on recent views). Used only when such links are integrated into a journal-wide template.";
    function print_user_content() "Layouts may call this at some point, and users can override it to add extra content to their journal view if their layout supports this.";
    function print_viewspecific_info() "Print view-specific information, if any (such as range information on recent views).  Used only when such content is integrated into a journal-wide template.";
}
class RecentPage extends Page { ### 'Most Recent Entries' page.
    # Class for 'lastn' view.
    var Entry[] entries;
    var RangeInfo range;
    function print_rangetext;  # Output text describing the current range
    function print_rangelinks; # Output some skiplinks
}
class FriendsPage extends RecentPage { ### Friends' most recent entries.
    var Friend{} friends;
}
class DayPage extends Page {
    var Date prevdate;
    var Date nextdate;
    var Link prevlink;
    var Link nextlink;
    var bool has_entries;
    var Entry[] entries;
    var Date viewdate;
}
class CalendarPageYear {        ### Information on how to link to a year in the calendar
    var int year;               # Number of the year, eg 2001.
    var string url;             # URL to link to for this year. ***
    var bool displayed;         # If this is the year currently being displayed, this will be true.
    function print_link();      # Print a link (or just text, if displayed=true) to this year.
}
class CalendarPageDay {
    var int number;             # Day of month number
    var Date date;              # A timeless date value which can be formatted
    var bool exists;            # If this day does not exist (ie, it's before or after the month) this will be false and all other values will be blank
    var int num_entries;        # Number of entries made on this day
    var string url;             # A URL to link to to view the day. ***
    function print_label;      # Print this number as it will appear in the calendar
    function print_forcalendar;# Print out a table cell (or something similar) to represent this day.
}
class CalendarPageWeek {        ### Represents a 'week' on the calendar
    var int pre_empty;          # How many days at the start of the week are blank? (From previous month)
    var int post_empty;         # How many days at the end of the week are blank? (From next month)
    var CalendarPageDay[] days; # An array of the days of the week (0=sunday)
    function print();
}
class CalendarPageMonth {       ### A month from the calendar page (compare CalendarMonthPage)
    var bool hasentries;        # If this is false, you probably don't want to display this month.
    var int number;             # The number of the month
    var int year;               # The number of the year
    function name() : string;   # Outputs the name of the month in natural language (eg: "January 1999", "Noviembre, 2000" or "Marzo 1233")
    var CalendarPageWeek[] weeks; # An array of the weeks of the month (for ease of building a row-per-week calendar)
    var string url;             # A url to link to in order to view this month. ***
}
class CalendarPage extends Page {
    var int year;               # The year being viewed
    var CalendarPageYear[] years; # Information for linking to other years
    var CalendarPageMonth[] months; # An array of months (indexed 1-12 to match the month)
    var int[] weekdays;      # Integers representing the days of the week. This will start on Monday or Sunday depending on the property setting for start-of-week
    function print_month(CalendarPageMonth m);
    function print_year_links();
}
class CalendarMonthPageDay extends CalendarPageDay {
    function print_subjectlist;# Print a list of entry summaries including subjects
    var Entry[] entries;        # Only populated on the month view. Entry text not present.
}
class CalendarMonthPageMonth extends CalendarPageMonth {
    var CalendarMonthPageDay[] days; # Array of the days of the month
}
class CalendarMonthPage extends Page {  ### A page which contains a list of posts made in that month
    var CalendarMonthPageMonth month;   # The month to be displayed
    var Link prev;                      # A link to the previous month, with image.
    var Link next;                      # A link to the next month, with image.
    function builtin print_navform();  # Print out a form which can be used to select a month and year.
    function set_link_text()
    "Call for translation of the text in the next and previous links. Should be overridden in the Core i18n layer, and will be called automatically before rendering begins.";
}
class UserinfoPage extends Page {
    var Link{} linkele;         # A collection of relevant links
    var string{} captions;      # A hash of strings for captioning the different information
    var string moreurl;         # A URL to link to for more information (eg http://www.livejournal.com/userinfo.bml?user=xxx&mode=full)
    function set_linkele_text()  "Call for translation/customisation of linkele link text. Should be overridden by Core i18n layers, and will be run automatically before rendering begins.";
    function set_caption_text()  "Call for translation/customisation of the captions. Should be overridden by Core i18n layers, and will be run automatically before rendering begins.";
}
class CommentPage extends Page {### A general talk page
    var Link prevlink;          # A link to the talkread page for the previous entry.
    var Link nextlink;          # A link to the talkread page for the next entry.
    var bool notallowedtoview;  # Set to true if the remote user does not have access to read this page.
    var bool canaddcomment;     # True if the remote user can leave a comment (don't display post link/form if false)
    var Link postlink;          # A link which should be used to start a new comment thread.
    var Link readlink;          # A link which should be used to read the discussion from the root
    var string talkthingtype;   # 'L' if replying to a journal entry, 'C' for comment
    function deniedtext();      # Print out text saying the user does not have access to view this page.
    function print_switchlink(); # Output a link to "Add a new comment" from commentread, or "Read Existing Comments" from commentpost
    function print_talkthing(); # Output whatever is being commented on
    function print_talkfunc(); # Output some 'functionality' HTML... that is, a comment thread, or a posting form.
    function print_extrahtml(); # Spit some extra HTML into the page depending on what the talkthing is (for example Topic Directory info)
}
class CommentReadPage extends CommentPage { ### A page for reading comments on something
    var Comment[] comments;     # An array of the comments to display
    var bool has_comments;       # Are there actually any comments to display?
    var bool root;              # True if rendering from the root of the comments, rather than rooted at a particular comment.
}
class CommentReadPageL extends CommentReadPage { ### A page for reading comments on entries
    var Entry entry;            # The entry whose comments are being viewed.
}
class CommentPostPage extends CommentPage { ### A page for posting comments
    var string{} form_strings;   # Some strings used to make the form
    function builtin output_form(); # Print out an HTML form for the end user to fill in in order to post.
    function set_form_text()     "[method[CommentPostPage::output_form()]] will call this before printing the form. Translate [member[CommentPostPage.form_strings]] here.";
}
class CommentPostPageL extends CommentPostPage { ### A page for posting comments to log entries
    var Entry entry;            # If $.replytotype="L", this contains the entry being replied to.
}
class CommentPostPageC extends CommentPostPage { ### A page for posting comments to other comments
    var Comment comment;        # If $.replytotype="C", this contains the comment being replied to.
}


# functions
function builtin eurl (string s) : string;              # URL escape
function builtin ehtml (string s) : string;             # HTML escape tags - leaves quotes intact
function builtin ehtmlattr (string s) : string;         # HTML escape - escapes quotes too

function builtin rand (int low, int high) : int
"Returns a random integer between \$low and \$high, inclusive.";

function builtin rand (int high) : int
"Returns a random integer between 1 and \$high, inclusive.";

function builtin alternate (string a, string b) : string; # With each call, this function will alternate between the two values and return one of them.
                                                          # Useful for making tables whose rows alternate in background color.
function builtin zeropad (int n,int digits) : string;   # Returns the number padded with zeroes so it has the amount of digits indicated.
function builtin zeropad (string n,int digits) : string;# Returns the number padded with zeroes so it has the amount of digits indicated.
function builtin striphtml (string s) : string;         # Similar to ehtml, but the HTML tags are stripped rather than escaped.
function builtin get_url(string user, string view) : string; # Returns a URL to the specified view for the specified user. Views use the same names as elsewhere. (recent, friends, calendar, userinfo)
function builtin get_url(string user, string view, int parameter) : string;    #  Two more forms of geturl which can return urls to views which
function builtin get_url(string user, string view, string parameter) : string; # require parameters, eg month or day view.
function builtin get_url(string user, string view, Date parameter) : string;   # Date for the calendar, month or day view.
function builtin get_url(UserLite user, string view) : string; # Returns a URL to the specified view for the specified user. Views use the same names as elsewhere. (recent, friends, calendar, userinfo)
function builtin get_url(UserLite user, string view, int parameter) : string;    #  Two more forms of geturl which can return urls to views which
function builtin get_url(UserLite user, string view, string parameter) : string; # require parameters, eg month or day view.
function builtin get_url(UserLite user, string view, Date parameter) : string;   # Date for the calendar, month or day view.
function builtin get_url(User user, string view) : string; # Returns a URL to the specified view for the specified user. Views use the same names as elsewhere. (recent, friends, calendar, userinfo)
function builtin get_url(User user, string view, int parameter) : string;    #  Two more forms of geturl which can return urls to views which
function builtin get_url(User user, string view, string parameter) : string; # require parameters, eg month or day view.
function builtin get_url(User user, string view, Date parameter) : string;   # Date for the calendar, month or day view.
function builtin gmttime : Date;                # Returns a date representing the current time in the GMT zone
function builtin servertime : Date;             # Returns a date representing the current time as the server sees it
function builtin join(string glue,string[] bits) : string;
function builtin split(string cut,string source) : string[];
function builtin push(string[] array,string newmember) : string[]; # Add newmember to the end of the array. (not in place, because that didn't work)
function builtin pop(string[] array) : string; # Return the last member of the array and remove it from the array
function builtin shift(string[] array) : string; # Return the first member of the array and remove it from the array.
function builtin unshift(string[] array,string newmember) : string[]; # Adds newmember to the front of the array,  (not in place, because that didn't work)
function builtin unichar(int code); # Returns a unicode character in UTF-8 format. (Use UTF-8 inplace where possible, though)
function builtin sort_hash_keys(int{} somehash) : string[];
function builtin sort_hash_keys(string{} somehash) : string[];
function builtin sort_link_keys(Link{} somelinks) : string[]; # Returns an array of hash keys in order of the 'title' of the link
function builtin get_account_name(int caps) : string; # Get a natural language account name for the given capabilities specifier. (Should be able to know language from backend)
function builtin get_userprops(string[] usernames,string[] propnames) : Properties{}; # Retrieve arbitrary userprops into a 'MultiProp' hash.
function builtin get_userprops(Friend{} friends,string[] propnames) : Properties{}; # Same as string[] version, but accepts a hash of friends for convenience.
function builtin get_userpics(string[] usernames) : UserPic{}; # Retrieve the default userpics for all users in the array.
function builtin get_userpics(Friend{} friends) : UserPic{};   # Same as string[] version, but accepts a hash of friends for convenience.
function builtin colorfromHSV(int hue, int saturation, int value) : Color; # Take percentage HSV values and form an RGB color (could be used to dynamically create a color spectrum or such?)

# s2compile has these built into it
function builtin string(int i) : string; # Cast an integer onto a string
function builtin int(string s) : int;    # Cast a string onto an integer

# Interaction with the HTTP server
function builtin set_content_type(string text);  #  Set the HTTP Content-type response header (for example, if outputting XML)
                                               # Must be called before printing any data.
# properties
property int page_recent_items {
    des = "Number of journal entries to show on recent entry page";
    min = 5;
    max = 50;
}
property int page_friends_items {
    des = "Number of journal entries to show on friends page";
    min = 5;
    max = 50;
}
set page_recent_items = 20;
set page_friends_items = 20;
property string page_day_sortorder {
    des = "Order of entries shown on a Day page";
    values = "forward|Least recent first|reverse|Most recent first";
}
property string page_calendar_sortorder {
    des = "Order of months shown on the Calendar page";
    values = "forward|Least recent first|reverse|Most recent first";
}
set page_day_sortorder = "forward";
set page_calendar_sortorder = "forward";
property string text_post_comment {
    des = "Link text to show to leave a comment";
    example = "Leave a comment";
    maxlength = 20;
}
property string text_read_comment {
    des = "Link text to show to read a single comment";
    example = "1 comment";
    maxlength = 20;
}
property string text_read_comments {
    des = "Text to show in a link to read comments";
    maxlength = 20;
    "size" = 15;
    example = "# comments";
    note = "Include a # character to insert the number of comments.";
}
property string text_read_comments_nonumber {
    des = "Text to show in a link to read comments when the number of comments isn't available";
    maxlength = 20;
    "size" = 15;
    example = "Read Comments";
}
set text_post_comment = "Add a comment";
set text_read_comment = "1 comment";
set text_read_comments = "# comments";
set text_read_comments_nonumber = "Read Comments";
property string text_skiplinks_back {
    des = "Text to show in a link to skip back through entries";
    maxlength = 20;
    "size" = 15;
    example = "Go back #";
    note = "Include a # character to insert the number of entries that will be viewable when skipping back.";
}
property string text_skiplinks_forward {
    des = "Text to show in a link to skip forward through entries";
    maxlength = 20;
    "size" = 15;
    example = "Go forward #";
    note = "Include a # character to insert the number of entries that will be viewable when skipping forward.";
}
property string text_skiplinks_forward_words {
    des = "Text to show in a link to skip forward through entries";
    maxlength = 20;
#    size = 15;
    example = "Go forward";
}
set text_skiplinks_back="Previous #";
set text_skiplinks_forward="Next #";
property string text_view_recent {
    des = "Text used to link to the 'Recent Entries' view.";
    maxlength = 20;
    "size" = 15;
    example = "Recent Posts";
}
property string text_view_friends {
    des = "Text used to link to the 'Friends' view.";
    maxlength = 20;
    "size" = 15;
    example = "My Friends' Entries";
}
property string text_view_calendar {
    des = "Text used to link to the 'Calendar' view.";
    maxlength = 20;
    "size" = 15;
    example = "Journal Archive";
}
property string text_view_userinfo {
    des = "Text used to link to the 'User Information' view.";
    maxlength = 20;
    "size" = 15;
    example = "My Profile";
}
property string text_view_month {
    des = "Text used to link to a list of subjects for a month.";
    maxlength = 20;
    "size" = 15;
    example = "View Subjects";
}
set text_view_recent = "Recent Entries";
set text_view_friends = "Friends";
set text_view_calendar = "Calendar";
set text_view_userinfo = "User Info";
set text_view_month = "View Subjects";
property string text_nosubject {
    des = "Text to replace a subject line when no subject is specified";
    maxlength = 20;
    size = 10;
    example = "No Subject";
    note = "This only appears in places where a subject line is required, such as on a month view.";
}
set text_nosubject = "No Subject";
property string text_noentries_recent {
    des = "Text do display when there are no entries on the recent or friends views";
    maxlength = 255;
    size = 50;
}
set text_noentries_recent = "There are no entries to display.";
property string text_noentries_day {
    des = "Text do display when there are no entries on the day view";
    maxlength = 255;
    size = 50;
}
set text_noentries_day = "There were no entries on this day.";
property string text_global_title {
    des = "Journal Title";
    maxlength = 50;
    size = 50;
    note = "Leave this field empty to use a default title.";
}
set text_global_title = "";
property string font_base {
    des = "Preferred Font";
    maxlength = 25;
    "size" = 10;
    example = "Arial";
    note = "Leave blank if you don't care.";
}
set font_base = "";   # In core, default is not to care. Layouts will probably specify fonts the author likes instead.
property string font_fallback {
    des = "Alternative font style";
    values = "sans-serif|Display in a sans-serif font|serif|Display in a serif font|cursive|Display in a cursive font|monospace|Display in a monospaced font|none|Use whatever font the user has configured in their browser";
    note = "This general style will serve as a fallback if your preferred font is unavailable.";
}
set font_fallback = "none"; # Default in core is to let the browser handle it.
property string reg_firstdayofweek {
    des = "Calendar weeks start on";
    values = "sunday|Sunday|monday|Monday";
}
set reg_firstdayofweek = "sunday";
property string text_day_prev {
    des = "Text to link to the previous day";
    example = "Previous Day";
    maxlength = 20;
}
property string text_day_next {
    des = "Text to link to the next day";
    example = "Next Day";
    maxlength = 20;
}
set text_day_prev = "Previous Day";
set text_day_next = "Next Day";

##  This being a string is ugly, but can't do "values" on an int, so I guess
## code will cast it to int later, or be written in perl and thus not care.
property string opt_entryheadingbase {
    des = "Entry base heading level";
    values="1|1|2|2|3|3|4|4|5|5|6|6";
    note = "When HTML heading tags are included in entries, they will be remapped to start from this heading level. If your style doesn't use HTML headings, you should set this to '1'.";
}
set opt_entryheadingbase = "4";

### functions

# general function definitions
function lang_monthname_long(int num) : string {
    if ($num == 1) { return "January"; }
    if ($num == 2) { return "February"; }
    if ($num == 3) { return "March"; }
    if ($num == 4) { return "April"; }
    if ($num == 5) { return "May"; }
    if ($num == 6) { return "June"; }
    if ($num == 7) { return "July"; }
    if ($num == 8) { return "August"; }
    if ($num == 9) { return "September"; }
    if ($num == 10) { return "October"; }
    if ($num == 11) { return "November"; }
    if ($num == 12) { return "December"; }
    return "???????";
}
function lang_monthname_short(int num) : string {
    if ($num == 1) { return "Jan"; }
    if ($num == 2) { return "Feb"; }
    if ($num == 3) { return "Mar"; }
    if ($num == 4) { return "Apr"; }
    if ($num == 5) { return "May"; }
    if ($num == 6) { return "Jun"; }
    if ($num == 7) { return "Jul"; }
    if ($num == 8) { return "Aug"; }
    if ($num == 9) { return "Sep"; }
    if ($num == 10) { return "Oct"; }
    if ($num == 11) { return "Nov"; }
    if ($num == 12) { return "Dec"; }
    return "???";
}
function lang_dayname_long(int num) : string {
    if ($num == 1) { return "Monday"; }
    if ($num == 2) { return "Tuesday"; }
    if ($num == 3) { return "Wednesday"; }
    if ($num == 4) { return "Thursday"; }
    if ($num == 5) { return "Friday"; }
    if ($num == 6) { return "Saturday"; }
    if ($num == 7) { return "Sunday"; }
    return "?????";
}
function lang_dayname_short(int num) : string {
    if ($num == 1) { return "Mon"; }
    if ($num == 2) { return "Tue"; }
    if ($num == 3) { return "Wed"; }
    if ($num == 4) { return "Thu"; }
    if ($num == 5) { return "Fri"; }
    if ($num == 6) { return "Sat"; }
    if ($num == 7) { return "Sun"; }
    return "???";
}
function lang_ordinal(string num) : string { # Make an ordinal number from a cardinal number
    if ($num->ends_with("11")) { return "11th"; }
    if ($num->ends_with("12")) { return "12th"; }
    if ($num->ends_with("13")) { return "13th"; }
    if ($num->ends_with("1")) { return $num+"st"; }
    if ($num->ends_with("2")) { return $num+"nd"; }
    if ($num->ends_with("3")) { return $num+"rd"; }
    return $num+"th";
}
function lang_ordinal(int num) : string { #  It's going to get converted anyway - this way
    return lang_ordinal($num->toString());  # we get some bonus flexibility out of it
}
function lang_viewname(string viewid) : string { # Get some words representing a view
    if ($viewid == "recent") { return $*text_view_recent; }
    if ($viewid == "calendar") { return $*text_view_calendar; }
    if ($viewid == "friends") { return $*text_view_friends; }
    if ($viewid == "day") { return "Day"; }
    if ($viewid == "month") { return "Month"; }
    if ($viewid == "userinfo") { return $*text_view_userinfo; }
    if ($viewid == "commentread") { return "Read Comments"; }
    if ($viewid == "commentpost") { return "Post Comment"; }
    return "Unknown View!";
}
function print_linkele(Link{} list) {
    # Output a string of image buttons to make a 'linkele'.
    "<div>\n";
    foreach var string l ($list) {
        "&nbsp;";
        $list{$l}->print_button();
        "&nbsp;";
    }
    "\n</div>\n";
}
function print_linklist(Link{} list, string active, string[] order) {
    # Output a list of textual links in a particular order
    "<ul>\n";
    foreach var string vl ($order) {
       if ($vl == $active) {
           "<li class=\"active\">";
           print ehtml($list{$vl}.text);
           "</li>\n";
       } else {
           "<li><a href=\""+ehtmlattr($list{$vl}.url)+"\">";
           print ehtml($list{$vl}.text);
           "</a></li>\n";
       }
    }
    "</ul>\n";
}
function print_linklist(Link{} list, string active) {
    print_linklist($list,$active,sort_link_keys($list));
}
function print_linklist(Link{} list) {
    print_linklist($list,"");
}
function print_linklist(Link{} list, string[] order) {
    print_linklist($list,"",$order);
}
function link_settext(Link{} links,string id,string text) { # Set the text for a link in an array only if it exists
    if ((defined $links{$id})) {
        $links{$id}.text=$text;
    }
}
function htmlattr(string name, string value) : string { # If the value isn't blank, return in HTML attribute format with a leading space. HTML is NOT escaped
    if ($value != "") {
        $name=$name->lower();   # XHTML requires lowercase attribute names.
        return " $name=\""+ehtmlattr($value)+"\"";
    } else {
        return "";
    }
}
function htmlattr(string name, int value) : string {
    # Pull the old type-changing hack. Fun huh?
    return htmlattr($name,""+$value);
}

function Date::date_short() : string {
    return zeropad($.month,2)+"-"+zeropad($.day,2)+"-"+zeropad($.year,4);
}
function Date::time_short() : string {
    return zeropad($.hour,2)+":"+zeropad($.minute,2);
}
function Date::time_long() : string {
    var int nicehour;
    var string dayhalf;
    if ($.hour > 12) {
        $nicehour=$.hour - 12;
        $dayhalf="PM";
    } else {
        if ($.hour == 0) {
            $nicehour=12;
        } else {
            $nicehour=$.hour;
        }
        $dayhalf="AM";
    }
    return $nicehour+":"+zeropad($.minute,2)+$dayhalf;
}
function Date::datetime_long() : string {
    return $this->date_long()+", "+$this->time_long();
}
function Date::datetime_short() : string {
    return $this->date_short()+" "+$this->time_short();
}
function Date::toString() : string {
    return $this->datetime_short();
}
function Date::date_long() : string {
    return lang_dayname_long($this->dayofweek())+" "+
           lang_monthname_long($.month)+" "+
           lang_ordinal($.day)+" "+$.year;
}


function userinfoimage(UserLite user) : Image {
    var Image uimage;
    $uimage.w=17;
    $uimage.h=17;
    $uimage.alt="[i]";
    $uimage.attributes{"align"}="absmiddle";
    $uimage.attributes{"border"}="0";
    $uimage.title="User Information";
    # This can be overridden in higher layers to select different icons
    if ($user.journaltype == "C") {
        $uimage.url="$*IMGDIR/community.gif";
    } elseif ($user.journaltype == "N") {
        $uimage.url="$*IMGDIR/newsinfo.gif";
    } else {
        $uimage.url="$*IMGDIR/userinfo.gif";
    }
    return $uimage;
}
function UserLite::toString(Color fg, Color bg, bool usefg, bool usebg) : string {
    var Image uiimg;
    $uiimg=userinfoimage($this);
    var string style;
    $style="";
    if ($usefg) { $style=$style+"color: $fg; "; }
    if ($usebg) { $style=$style+"background: $bg;"; }
    return "<a"+htmlattr("style",$style)+" href=\""+get_url($this,"userinfo")+"\">$uiimg</a>"+
           "<b><a"+htmlattr("style",$style)+htmlattr("title",$.name)+
           " href=\""+$.baseurl+"\">$.username</a></b>";
}
function UserLite::toString() : string {
    var Color dummy;
    return $this->toString($dummy,$dummy,false,false);
}
function UserLite::print() {
    print $this->toString();
}
function UserLite::print(Color fg) {
    print $this->toString($fg,$fg,true,false);
}
function UserLite::print(Color fg, Color bg) {
    print $this->toString($fg,$bg,true,true);
}
function Page::view_title() : string {
    if ($.view == "friends") {
        if ($.journal.journaltype == "C") {
            return "Members' Entries";
        } else {
            return "Friends";
        }
    }

    return lang_viewname($.view);
}
function FriendsPage::view_title() : string {
    if ($.journal.journaltype == "C") {
        return "Members' Journals";
    } else {
        return "Friends";
    }
}
function DayPage::view_title : string {
    return $.viewdate->date_short();
}
function CalendarPage::view_title() : string {
    return $.year->toString();
}
function UserinfoPage::view_title : string {
    if ($.journal.journaltype=="P") {
        return "Profile";
    } else {
        return "Information";
    }
}
function Page::title() : string {
    return $.global_title+($.view != "recent" ? " - "+$this->view_title() : "");
}
function livejournal_signature() {
    """Powered by <a href="$*SITEURL">$*SITENAME</a>""";
}
function Page::print() {
    """<html lang="$.lang" dir="$.textdirection">\n<head>\n<title>""";
    print $this->title();
    "</title>\n$.head_content";
    $this->print_head();
    if ($*font_base != "" or $*font_fallback != "none") {
        """<style type="text/css"><!--\nbody, td, th, table, p, div {\n""";
        "font-family: ";
        if ($*font_base != "") {
            "\"$*font_base\"";
            if ($*font_fallback != "none") {
                ", ";
            }
        }
        if ($*font_fallback != "none") {
            print $*font_fallback;
        }
        ";\n}\n--></style>\n";
    }
    "</head>\n<body>\n";
    """<img src="$.journal.defaultpic.url" width="$.journal.defaultpic.width" """;
    """height="$.journal.defaultpic.height" align="right"><h1>""";
    print $.global_title;
    "</h1>\n";
    $this->set_view_text();
    print_linklist($.views,$.view,$.viewsorder);
    $this->print_body();
    """\n<hr><address>""";
    livejournal_signature();
    """</address>\n</body></html>""";
}
function Page::print_viewspecific_links() {
    # Blank in core!
}
function Page::print_viewspecific_info() {
    # Blank in core!
}
function Page::print_user_content() {
    # Blank in core!
}
function Page::set_view_text() {
    # This function should be overridden in i18nc, but these
    # four should still be set by properties.
    link_settext($.views,"recent",$*text_view_recent);
    link_settext($.views,"friends",$*text_view_friends);
    link_settext($.views,"calendar",$*text_view_calendar);
    link_settext($.views,"userinfo",$*text_view_userinfo);
    # i18nc and user layers can redefine this to change views which don't have properties
}

### Generic Page Functions
function Page::print_body() {
    """<p>Error: There is no body renderer for viewtype <tt>$.view</tt> defined.</p>""";
}
function Page::print_head() {
    # Override me. I resemble GLOBAL_HEAD!
    return;
}
function MetaData::print() {
    "<div class=\""+$.key+"\"><strong>"+$this->indicator()+":</strong> "+$.val+"</div>\n";
}
function MetaData::indicator : string {
    if ($.key == "currentmusic") {
        return "Current Music";
    }
    if ($.key == "currentmood") {
        return "Current Mood";
    }
    # Metadata we don't know about yet...
    return $.key;
}
function Page::print_entry_poster(Entry e) {
    $e.poster->print();
    if ($e.poster.userid != $e.journal.userid) {
        " posting in ";
        $e.journal->print();
    }
    " at "+$e.entrytime->time_long();
}
function Page::print_entry(Entry e) {
    ## For most styles, this will be overridden by FriendsPage::print_entry and such.
    """<div class="entry" id="entry$e.itemid" lang="$e.lang" dir="$e.textdirection">\n""";
    "<h3>";
    $this->print_entry_poster($e);
    if (defined $e.subject) {
        ": $e.subject";
    }
    if ($e.has_security) {
        " "+$e.securityicon;
    }
    "</h3>\n";
    """<div class="entrytext">\n$e.text\n</div>\n""";
    $e->print_metadata();
    if ($e.commentinfo.enabled) {
        $e.commentinfo->print();
    }
    "</div>\n\n";
}
function FriendsPage::print_entry(Entry e) {
    ## For most styles, this will be overridden by FriendsPage::print_entry and such.
    """<div class="entry" id="entry$e.itemid">\n""";
    var Color bg;
    var Color fg;
    $bg=$.friends{$e.journal.username}.bgcolor;
    $fg=$.friends{$e.journal.username}.fgcolor;
    "<h3 style=\"color: "+$fg->toString()+"; background: "+$bg->toString()+" none;\">";
    $this->print_entry_poster($e);
    if (defined $e.subject) {
        ": $e.subject";
    }
    if ($e.has_security) {
        " "+$e.securityicon;
    }
    "</h3>\n";
    """<div class="entrytext">\n$e.text\n</div>\n""";
    $e->print_metadata();
    if ($e.commentinfo.enabled) {
        $e.commentinfo->print();
    }
    "</div>\n\n";
}
function Entry::print_metadata() {
    if ($.has_metadata) {
        "<div class=\"metadata\">\n";
        foreach var string m ($.metadata) {
            $.metadata{$m}->print();
        }
        "</div>\n";
    }
}

### RecentPage and related functions
function RecentPage::print_body {
    # Creator for both the Recent and Friends views, since they are similar
    # If someone wants to do the two views differently, they can create
    # FriendsPage::print_body since FriendsPage extends RecentPage.
    "<p>";
    $this->print_rangetext();
    "</p>\n";
    $this->print_rangelinks();
    foreach var Entry e ($.entries) {
        if ($e.end_day) {
            "</div>";
        }
        if ($e.new_day) {
            """<div class="day" id="day"""+$e.entrytime->date_uniq()+"""\">\n<h2>""";
            print $e.entrytime->date_long();
            "</h2>\n";
        }
        # Print the entry
        $this->print_entry($e);
    }
    $this->print_rangelinks();
}
function RecentPage::print_rangetext {
    """You are viewing """;
    if ($.range.skipped>0) {
       """$.range.shown entries starting $.range.skipped into the past.""";
    } else {
       """the $.range.shown most recent entries.""";
    }
}
function RecentPage::print_rangelinks {
    if ($.range.backnum == 0 and $.range.forwardnum == 0) {
        return;
    }
    """<div class="skiplinks">\n[""";
    if ($.range.backnum > 0) {
        """<a href="$.range.backurl">&lt;&lt; """;
        print $*text_skiplinks_back->insert_number($.range.backnum);
        """</a>""";
    }
    if ($.range.backnum > 0 and $.range.forwardnum > 0) {
        """ | """;
    }
    if ($.range.forwardnum > 0) {
        """<a href="$.range.forwardurl">""";
        print $*text_skiplinks_forward->insert_number($.range.forwardnum);
        """ &gt;&gt;</a>""";
    }
    "]</div>";
}

### UserinfoPage
function userinfo_row_pre(string title) {
    # Prints out the HTML for a userinfo row, up to the point where the data goes.
    "<tr><th>"+$title+":</th><td>";
}
function userinfo_row_post {
    # Prints out the HTML to end a userinfo row
    "</td></tr>\n";
}
function userinfo_row(string title, string data) {
    # Prints out a 'row' of userinfo.
    userinfo_row_pre($title);
    print $data;
    userinfo_row_post();
}
function UserinfoPage::print_body {
    # View renderer for the UserInfo page
    $this->set_caption_text();     # Call for translation of caption text
    $this->set_linkele_text();     # Call for translation of the link text if appropriate
    print_linkele($.linkele);
    """<table width="100%" cellpadding="5" style="margin-top: 2em;">\n""";
    userinfo_row($.captions{"username"},"""<a href="$.journal.baseurl">$.journal.username</a>""");
    userinfo_row($.captions{"name"},$.journal.name);
    if ($.journal.websiteurl != "") {
        userinfo_row($.captions{"website"},"""<a href="$.journal.websiteurl">"""+($.journal.websitename!="" ? $.journal.websitename : $.journal.websiteurl)+"""</a>""");
    }
    if ($.journal.journaltype == "P") {
        if ($.journal.location != "") {
            userinfo_row($.captions{"location"},$.journal.location);
        }
        if ($.journal.birthday->date_uniq() != "00000000") {
            userinfo_row($.captions{"birthday"},$.journal.birthday->date_short());
        }
    }
    if ($.journal.email != "") {
        userinfo_row($.captions{"email"},$.journal.email);
    }
    var string bio;
    $bio = $.journal->getbio();
    if ($bio != "") {
        userinfo_row($.captions{"bio"},$bio);
    }
    # These require foreach loops, so we can't use userinfo_row
    if ($.journal.hasinterests) {
        userinfo_row_pre($.captions{"interests"});
        var Interest[] intr;
        $intr = $.journal->getinterests();
        foreach var Interest i ($intr) {
            if ($i.count > 0) {
                """<a href="$i.url">""";
            }
            if ($i.shared) {
                "<b>";
            }
            print $i.name;
            if ($i.shared) {
                "</b>";
            }
            if ($i.count > 0) {
                "</a>";
            }
        }
        userinfo_row_post();
    }

    var Friend{} fr;
    if ($.journal.hasfriends) {
        userinfo_row_pre($.captions{"friends"});
        $fr = $.journal->getfriends();
        foreach var string f ($fr) {
            "<a href=\""+get_url($fr{$f}.username,"userinfo")+"\">"+$fr{$f}.username+"</a> ";
        }
        userinfo_row_post();
    }
    if ($.journal.hasfriendof) {
        userinfo_row_pre($.captions{"friendofs"});
        $fr = $.journal->getfriendof();
        foreach var string f ($fr) {
            "<a href=\""+get_url($fr{$f}.username,"userinfo")+"\">"+$fr{$f}.username+"</a> ";
        }
        userinfo_row_post();
    }

    "</table>\n";
    if ($.moreurl != "") {
        "<div align=\"center\"><a href=\""+ehtmlattr($.moreurl)+"\">More details...</a></div>";
    }
}
function UserinfoPage::set_linkele_text() {
    # Blank in core. Override server-set values in other layers.
}
function UserinfoPage::set_caption_text() {
    # Blank in core. Override server-set values in other layers.
}

### All Comment Pages
function CommentPage::print_entry_poster(Entry e) {
    # Note that this does not include the time
    "$e.poster.name (";
        $e.poster->print(); ")";
        if ($e.poster.userid != $e.journal.userid) {
            " (posting in ";
            $e.journal->print();
            ")";
        }
    " wrote:";
}
function CommentPage::print_entry(Entry e) {
    """<div id="entry$e.uniqueid">\n""";
    if ($e.userpic.url != "") {
        """<table>\n<tr>""";
        """<td><img src="$e.userpic.url" width="$e.userpic.width" height="$e.userpic.height" alt="" title="$e.poster.name"></td>\n""";
        """<td>""";
    }
    "<div>";
    if ($e.has_security) {
        print "$e.securityicon ";
    }
    $this->print_entry_poster($e); "</div>\n";
    "<div style=\"font-size: 0.75em;\">"+$e.entrytime->datetime_long()+"</div>\n";
    if ($e.userpic.url != "") {
        "</td></tr></table>\n";
    }
    "<blockquote>";
    if (defined $e.subject) {
        "<div style=\"font-size: 1.1em; font-weight: bold;\">$e.subject</div>";
    }
    """<div>\n$e.text\n</div>\n""";
    $e->print_metadata();

    "</blockquote>\n\n";
}
function CommentPage::deniedtext() {
    "Sorry, you do not have access to view or take part in this discussion.";
}
function CommentPage::print_body() { ### Renderer for a comment view.
    if ($.notallowedtoview) {
        "<p>";
        $this->deniedtext();
        "</p>\n";
        return;
    }

    # Print out whatever it is that the comment thread is for
    $this->print_talkthing();
    $this->print_extrahtml();

    $this->print_switchlink();

    $this->print_talkfunc();

}
function CommentPage::print_extrahtml() {
    # "Generic" talk views don't get any extra HTML, of course.
    return;
}
function CommentPage::print_talkthing() {
    """<p>For some reason I can't render the discussion item. (This is a bug)</p>""";
}
function CommentPage::print_talkfunc() {
    """<p>I don't know how to deal with the functionality of a <tt>$.view</tt> view. (This is a bug)</p>""";
}
function CommentReadPage::print_talkfunc() {
    if ($.has_comments) {
        """<div id="comments">\n""";
        foreach var Comment c ($.comments) {
            $c->print();
        }
        "</div>";
    }
}
function CommentPostPage::print_talkfunc() {
    $this->output_form();
}
#function CommentPostPage::output_form() {
#    "<p>Error: Don't run me! I should be <code>builtin</code>!</p>";
#}
function CommentReadPageL::print_talkthing() {
    if ($.root) {
        $this->print_entry($.entry);
    } else {
        $this->print_entry_summary($.entry);
    }
}
function CommentPostPageL::print_talkthing() {
    $this->print_entry($.entry);
}
function CommentPostPageC::print_talkthing() {
    $.comment->print();
}

function CommentPage::print_switchlink() {
    """<div style="text-align: center;">(""";
    if ($.view == "commentread") {
        """<a href="$.postlink.url">"""+ehtml($*text_post_comment)+"</a>";
    } else {
        """<a href="$.readlink.url">"""+ehtml($*text_read_comments_nonumber)+"</a>";
    }
    """)</div>\n""";
}

### Calendar view
function CalendarPage::print_body {
    $this->print_year_links();
    foreach var CalendarPageMonth m ($.months) {
        $this->print_month($m);
    }
}
function CalendarPage::print_year_links() {
    """<ul>\n""";
    foreach var CalendarPageYear y ($.years) {
        if ($y.displayed) {
            """<li class="active">$y.year</li>\n""";
        } else {
            """<li><a href="$y.url">$y.year</a></li>\n""";
        }
    }
    """</ul>\n""";
}
function CalendarPage::print_month(CalendarPageMonth m) {
        if (not $m.hasentries) { return; }
        """\n<table style="margin-left: 25%; margin-right: 25%; margin-top: 1em; margin-bottom: 1em; border-collapse: collapse; border: 1px solid;" border="1">\n""";
        """<tr><th colspan="7" style="text-align: center; border: 1px solid;">""";
        print $m->name();
        """</th></tr>\n""";
        # Output the day headings
        foreach var int d ($.weekdays) {
            "<th>"+lang_dayname_short($d)+"</th>\n";
        }
        "</tr>\n";
        foreach var CalendarPageWeek w ($m.weeks) {
            $w->print();
        }
        """<tr><td colspan="7" style="text-align: center; border: 1px solid;">
           <a href="$m.url">$*text_view_month</a></td></tr>\n""";
        "</table>";
}
function CalendarPageMonth::name() : string {
    var string monthtitle = lang_monthname_long($.number)+" $.year";
    return $monthtitle->upperfirst();
}
function CalendarPageWeek::print() {
   """<tr valign="top" style="height: 2em;">\n""";
   if ($.pre_empty > 0) {
      """<td class="emptyday" colspan="$.pre_empty">&nbsp;</td>\n""";
   }
   foreach var CalendarPageDay d ($.days) {
      $d->print_forcalendar();
   }
   if ($.post_empty > 0) {
      """<td colspan="$.post_empty">&nbsp;</td>\n""";
   }
}
function CalendarPageDay::print_forcalendar() {
    if (not $.exists) {  # Is the day before or after the month?
        return;
    }
    """<td style="border: 1px solid;">\n""";
    """<div style="text-align: right;">$.number</div>\n""";
    if ($.num_entries > 0) {
        """<div style="text-align: center;"><a href="$.url">$.num_entries</a></div>\n""";
    }
    """</td>\n""";
}
function CalendarMonthPage::print_body {
    $this->set_link_text();
    "<div class=\"monthnav\">";
    $.prev->print_button();
    $this->print_navform();
    $.next->print_button();
    "</div>\n<dl>";
    foreach var CalendarMonthPageDay d ($.month.days) {
        "<dt><a href=\"$d.url\">";
        $d->print_label();
        "</a></dt>\n<dd>";
        $d->print_subjectlist();
        "</dd>\n";
    }
    "</dl>\n";
}
function CalendarMonthPageDay::print_label() {
    print lang_ordinal($.number);
}
function CalendarMonthPage::set_link_text() {
    # Blank in core.
}
function CalendarMonthPageDay::print_subjectlist() {
    "<table>\n";
    foreach var Entry e ($.entries) {
        "<tr><td>";
        $e.entrytime->time_short();
        "</td><td>";
        if ($e.poster.userid != $e.journal.userid) {
            $e.poster->print();
        }
        "</td><td>";
        if ($e.subject != "") {
            "<a href=\"$e.viewurl\">"+striphtml($e.subject)+"</a>";
        } else {
            "<a href=\"$e.viewurl\" style=\"font-style: italic;\">($*text_nosubject)</a>";
        }
        "</td></tr>\n";
    }
    "</table>";
}

### Day view
function DayPage::print_body() {
    if ($.has_entries) {
        "<div class=\"day\" id=\"day"+$.viewdate->date_uniq()+"\">\n<h2>";
        print $.viewdate->date_long();
        "</h2>\n";

        foreach var Entry e ($.entries) {
            $this->print_entry($e);
        }

        "</div>";
    } else {
        "<p>"+ehtmlattr($*text_noentries_day)+"</p>";
    }

    "<div class=\"skiplinks\">\n";
    "<a href=\""+ehtmlattr($.prevlink.url)+"\">"+ehtmlattr($*text_day_prev)+"</a> | ";
    "<a href=\""+ehtmlattr($.prevlink.url)+"\">"+ehtmlattr($*text_day_next)+"</a>\n</div>";

}


### Functions from the Comment class
function Comment::set_link_text() {
    # Blank in core... server supplies stuff.
    return;
}
function Comment::print() {
    if ($.collapsed) {
        $this->print_collapsed();
    } else {
        $this->print_full();
    }
}
function Comment::print_full() {
    $this->set_link_text();
    "<div";
    if ($.indent > 0) {
        var int indentpx;
        $indentpx=$.indent * 50;
        " style=\"margin-left: "+$indentpx+"px;\"";
    }
    ">\n";
    if ($.userpic.url != "") {
        "<table><tr><td style=\"vertical-align: middle;\">\n";
        $.userpic->print();
        "</td><td style=\"vertical-align: top;\">";
    }
    if ($.subject != "") {
        "<div style=\"font-size: 1.1em; font-weight: bold;\">$.subject</div>\n";
    }
    """<div>""";
    $this->print_taglinetext();
    "</div>\n";
    print_linkele($.links);
    if ($.userpic.url != "") {
        "</td></tr></table>\n";
    }
    if ($.text != "") {
        """<div style="margin-top: 0.5em;margin-bottom: 0.5em;">$.text</div>\n""";
    }
    """<div style="font-size: 0.75em;">""";
    """<a href="$.replylink.url">($.replylink.text)</a>""";
    if ($.collapsemode) {
        """ <a href="$.threadlink.url">($.threadlink.text)</a>""";
        if ($.parenttalkid != 0) {
            """ <a href="$.parentlink.url">($.parentlink.text)</a>""";
        }
    }
    "</div>\n</div>\n\n";
}
function Comment::print_collapsed() {
    $this->set_link_text();
    "<div";
    if ($.indent > 0) {
        var int indentpx;
        $indentpx=$.indent * 50;
        " style=\"margin-left: "+$indentpx+"px;\"";
    }
    ">\n";
    var string subj;
    if ($.subject != "") {
        $subj=striphtml($.subject);
    } else {
        $subj="<span style=\"font-style: italic;\">($*text_nosubject)</span>";
    }
    "$subj - ";
    $.poster->print();
    " "+$.servertime->datetime_short()+"</div>\n\n";
}
function Comment::print_taglinetext() {
    "posted by ";
    $.poster->print();
    " at "+$.servertime->datetime_short();
}

### UserPic functions
function UserPic::print() {
    print $this->toString();
}
function UserPic::print(string align) {
    print $this->toString($align);
}
function UserPic::toString() : string {
    return $this->toString("");
}
function UserPic::toString(string align) : string  {
    var string ret;
    $ret="""<img src="$.url" width="$.width" height="$.height" alt="" """;
    if ($align != "") {
        $ret=$ret+"""align="$align" """;
    }
    $ret=$ret+">";
    return $ret;
}

### CommentInfo functions
function CommentInfo::print_readlink {
    if ($.comment_count == 1) {
        "<a href=\"$.url_read\">"+ehtml($*text_read_comment)+"</a>";
    } else {
        "<a href=\"$.url_read\">";
        print ehtml($*text_read_comments->insert_number($.comment_count));
        "</a>";
    }
}
function CommentInfo::print_postlink() {
    "<a href=\"$.url_post\">"+ehtml($*text_post_comment)+"</a>";
}
function CommentInfo::print() {
    if (not $.enabled) { return; }
    """<div style="text-align: right;">\n(""";
    if ($.comment_count > 0) {
        $this->print_readlink();
        " | ";
    }
    $this->print_postlink();
    ")</div>";
}

### Link object functions
function Link::print_button {
    if ($.url == "") { return; }
    "<a href=\""+ehtmlattr($.url)+"\">";
    $.icon.alt="[" + $.text + "]";
    $.icon.title=$.text;
    $.icon.attributes{"border"}="0";
    $.icon->print();
    "</a>";
}
function Link::print_text {
    if ($.url == "") { return; }
    print $this->toString();
}
function Link::toString() : string {
    return "<a href=\""+ehtmlattr($.url)+"\">"+ehtml($.text)+"</a>";
}

### Image object functions
function Image::print() {
    print $this->toString();
}
function Image::toString() : string {

    if ($.url == "") {
       return ehtml($.alt);
    }
    var string img;
    $img="";
    $img=$img+"<img src=\""+ehtml($.url)+"\"";
    $img=$img+htmlattr("height",$.h);
    $img=$img+htmlattr("width",$.w);
    $img=$img+" alt=\""+ehtmlattr($.alt)+"\""; # ALT is required, so output it even if it's blank.
    $img=$img+htmlattr("title",ehtmlattr($.title));
    $img=$img+htmlattr("longdesc",ehtmlattr($.longdesc));
    foreach var string attr ($.attributes) {
        $img=$img+htmlattr($attr,$.attributes{$attr});
    }
    $img=$img+">";

    return $img;
}

### Gender object
function Gender::toString() : string {
    if ($.internalname == "M") {
        return "male";
    } elseif ($.internalname == "F") {
        return "female";
    } elseif ($.internalname == "") {
        return "unspecified";
    } else {
        return "unknown gender";
    }
}
function Gender::name() : string {
    return $this->toString();
}
function Gender::posessive() : string {
    if ($.internalname == "M") {
        return "his";
    } elseif ($.internalname == "F") {
        return "her";
    } else {
        return "their";
    }
}
function Gender::pronoun() : string {
    if ($.internalname == "M") {
        return "he";
    } elseif ($.internalname == "F") {
        return "she";
    } else {
        return "they";
    }
}


### PortalBoxes
function PortalBox::print(string size) {
    "<div class=\"portalbox\">\n<h3>\n$.title\n</h3>\n";
    $this->print_content($size);
    "\n</div>\n";
}
function PortalBox::print() {
    $this->print("small");
}

## Some toString methods...
function Friend::toString() : string {
   return $.info->toString();
}


## Function to generate the stylesheet view...
function external_stylesheet() {
    "/* No Stylesheet Defined */\n";
}


### HTML Cleaner hooks ###

## <lj user="..."> and <lj comm="...">
# user is a pretty-empty UserLite object containing only username and journaltype
function genhtml_journallink(UserLite user) {
    $user->print();
}

## <lj-cut>
# link is an iconless Link object
function genhtml_cutlink(Link link) {
    "<b>(&nbsp;<a href=\""+ehtmlattr($link.url)+"\">"+ehtmlattr($link.text)+"</a>&nbsp;)</b>\n";
}

## Poll stuff
# Entertaining the idea of defining some classes for polls, but I don't think S2's
# OO is flexible enough to let it work. For now, this ugly hack works.
#   Execution order:
#     prepoll
#     (automatic insertion of form start tag which users cannot change)
#     prequestion
#     pollbar*numanswers, or server-generated HTML form element(s)
#     postquestion
#     ...repeat prequestion-postquestion for each question
#     (automatic insertion of form end tag which users cannot change)
#     postpoll
function genhtml_poll_prepoll(Link polllink, string title) {

}
function genhtml_poll_postpoll(Link polllink, string title) {

}
function genhtml_poll_prequestion(string question,string answerlink) {
    # answerlink might be empty, in which case a link should not
    # be output.
}
function genhtml_poll_postquestion(string question,string answerlink) {

}
function genhtml_poll_bar(string answertext,int count,int max,int percent) {
    # percent = count * 100 / max , for convenience, since it's not an expensive calculation
    # max is the value of the most popular answer to this question
}
