<?_code

 $title = "Change Password";
 $body = "";
 
 if ($LJ::SERVER_DOWN) {
     $body = LJ::server_down_html();
     $title = "Sorry...";
     return;
 }
 
 if ($FORM{'mode'} eq 'submit') 
 {
     my $user = LJ::canonical_username($FORM{'user'});
     my $password = $FORM{'password'};
     my $newpass1 = LJ::trim($FORM{'newpass1'});
     my $newpass2 = LJ::trim($FORM{'newpass2'});

     my $remote = LJ::get_remote();
     my $u = LJ::load_user($user);

     my @errors = ();
     if ($user eq "test") { push @errors, "Cannot change the test account's password."; }
     unless ($user) {
         push @errors, "You must enter your username."; 
     } else {
         unless (defined $u) {
             push @errors, "Invalid user <B>$user</B>.  This user does not exist.  Are you sure you typed it correctly?";
         } else {
             if (LJ::login_ip_banned($u)) {
                 push @errors, $ML{'error.ipbanned'};
             } elsif ($u->{'password'} eq "" || $u->{'password'} ne $password) {
                 push @errors, "Your old password is not correct.";
                 LJ::handle_bad_login($u);
             }
         }
     }
     if ($newpass1 ne $newpass2) {
         push @errors, "Your new passwords do not match.  Retype both again... you made a typo on one of them.";
     } else {
         if ($newpass1 eq "") {
             push @errors, "Your new password cannot be blank.";
         } elsif (length $newpass1 > 30) {
             push @errors, "Passwords are limited to a maximum of 30 characters.";
         } else {
             
             my @checkpass = LJ::run_hooks("bad_password", 
                                           { 'user' => $u->{'user'}, 'password' => $newpass1,
                                             'name' => $u->{'name'}, 'email' => $u->{'email'} });
             if (@checkpass && $checkpass[0]->[0]) {
                 push @errors, "Bad new password: $checkpass[0]->[0]";
             }
         }
     }

     # don't allow changes if email address is not validated
     unless ($u->{'status'} eq 'A') {
         push @errors, "You cannot change your password if your current email address has not been validated.";
     }

     unless (LJ::is_ascii($newpass1)) {
         push @errors, "Passwords are restricted to ASCII symbols. Please choose a password that does not use non-ASCII symbols.";
     }
     
     if (@errors) {
         $body = LJ::bad_input(@errors);
         return;
     }
     
     ## make note of changed password
     my $dbh = LJ::get_db_writer();
     my $oldval = $dbh->quote(Digest::MD5::md5_hex($u->{'password'} . "change"));
     $dbh->do("INSERT INTO infohistory (userid, what, oldvalue, timechange) VALUES ($u->{'userid'}, 'password', $oldval, NOW())");
     
     LJ::update_user($u, { password => $FORM{'newpass1'} });

     # Kill all sessions, forcing user to relogin
     LJ::kill_all_sessions($u);
     
     LJ::send_mail({
         'to' => $u->{'email'},
         'from' => $LJ::ADMIN_EMAIL,
         'fromname' => $LJ::SITENAME,
         'charset' => 'utf-8',
         'subject' => "Change of Password",
         'body' => ("Your password has been changed at $LJ::SITENAME.\n\n".
                    "To retrieve it in the future, visit:\n\n".
                    "  $LJ::SITEROOT/lostinfo.bml\n\n".
                    "Regards,\n$LJ::SITENAME Team\n\n$LJ::SITEROOT/\n")});
     
     $body = "<?h1 Success h1?><?p Your password has been changed and email has been sent to you with a reminder message. p?>";
     
     # if they were logged in, tell them to relogin
     $body .= "<?p In addition, you have been logged out from all your existing sessions. You should <a href='/login.bml'>login again</a> before continuing. p?>" if $remote;

     LJ::run_hooks("post_changepassword", {
         "u" => $u,
         "newpassword" => $FORM{'newpass1'},
         "oldpassword" => $u->{'password'},
     });

     return;
 }

 # else, show the form to change:
 $body .= "<form action='changepassword.bml' method='post'>\n";
 $body .= "<input type='hidden' name='mode' value='submit' />\n";

 $body .= "<?h1 Change Password h1?>\n";
 $body .= "<?p\n";
 $body .= "Fill out the form below to change your password.\n";
 $body .= "p?>\n";
 
 $body .= "<?standout\n";
 $body .= "Username:<br />\n";

 # we make the field for the new password *longer* than the max length
 # for a password - that way we can tell if someone is trying to use an
 # excessively long password, instead of silently truncating it.

 my $remote = LJ::get_remote_noauth();
 my $hval = LJ::ehtml($remote ? $remote->{'user'} : "");
 $body .= "<input name='user' size='30' maxlength='15' value='$hval' /><br />\n";
 $body .= "Old Password:<br />\n";
 $body .= "<input type='password' name='password' size='30' maxlength='30' /><br />\n";
 $body .= "New Password:<br />\n";
 $body .= "<input type='password' name='newpass1' size='30' maxlength='31' /><br />\n";
 $body .= "New Password (again):<br />\n";
 $body .= "<input type='password' name='newpass2' size='30' maxlength='31' /><br />\n";
 $body .= "standout?>\n";

 $body .= "<?h1 Proceed h1?>\n";
 $body .= "<?p\n";
 $body .= "Press the button below and your password will be changed. You will receive an email as well, noting the password change. \n";
 $body .= "p?>\n";

 $body .= "<?standout\n";
 $body .= "<input type='submit' value='Proceed' />\n";
 $body .= "standout?>\n";
 $body .= "</form>\n";
 return;

_code?><?page
title=><?_code return $title; _code?>
body=><?_code return $body; _code?>
page?><?_c <LJDEP>
post: htdocs/changepassword.bml
lib: Digest::MD5
hook: post_changepassword
</LJDEP> _c?>
